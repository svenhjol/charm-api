plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// Load props from parent gradle.properties.
def props = new Properties()
file("common/gradle.properties").withInputStream { props.load(it) }

archivesBaseName = "${props.mod_id}-fabric-${props.minecraft_version}"
version = props.mod_version
group = props.maven_group

sourceSets {
    main {
        java {
            srcDir "common/src/main/java"
        }  
        resources {
            srcDir 'src/main/resources'
        }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/${props.mod_id}.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${props.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${props.loader_version}"

    // Uncomment this to require foundation in child mods.
    // modImplementation "svenhjol.charm:charm-foundation-fabric-${props.minecraft_version}:${props.foundation_version}"

    // This is required for javax.annotation.Nullable to work properly.
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    mappings loom.layered {
        officialMojangMappings()
    }
}

processResources {
    inputs.property "version", props.mod_version

    filesMatching("fabric.mod.json") {
        expand "version": props.mod_version
    }

    filesMatching([props.mod_id + ".mixins.json", props.mod_id + ".fabric.mixins.json"]) {
        expand "refmap_file": props.mod_id + "-fabric-${props.minecraft_version}" + "-refmap.json"
    }
    
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
